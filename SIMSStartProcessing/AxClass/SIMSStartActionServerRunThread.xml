<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SIMSStartActionServerRunThread</Name>
	<SourceCode>
		<Declaration><![CDATA[
class SIMSStartActionServerRunThread extends RunBaseBatch implements BatchRetryable
{
    int     dummy;
    List    delLine = new List(Types::Int64);

    #define.CurrentVersion(1)
    #localmacro.CurrentList
        dummy
    #endmacro

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>canGoBatchJournal</Name>
				<Source><![CDATA[
    public boolean canGoBatchJournal()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    public boolean init()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        return [#CurrentVersion,#CurrentList];
    }

]]></Source>
			</Method>
			<Method>
				<Name>update</Name>
				<Source><![CDATA[
    protected void update()
    {
        SIMSStartTable      startTable;
        SIMSStartRunAction  startRunAction;
        int                 cnt;

        while select startTable
            order by SortOrder
            where startTable.RunOnStart == NoYes::Yes
        {
            SIMSStartLog::addLogFromStartTable(startTable, "@SIMSStartProcessing:StartProcessing");
            
            if (startTable.StartAllTime == NoYes::No && startTable.ExecutedOn)
            {
                SIMSStartLog::addLogFromStartTable(startTable, strFmt("@SIMSStartProcessing:StartAllTimeExecutedOnSkipedRecordProcessing", startTable.StartAllTime, startTable.ExecutedOn));
                continue;
            }

            if (!startTable.checkIfLineCanRun())
            {
                SIMSStartLog::addLogFromStartTable(startTable, strFmt("@SIMSStartProcessing:URLTemplateNotLikeURLSkipedRecordProcessing", startTable.URLTemplate, startTable.getEnvUrl()));
                continue;
            }

            try
            {
                if (startTable.DeleteLine == NoYes::Yes)
                {
                    delLine.addEnd(startTable.RecId);
                }
                
                cnt++;
                SIMSStartLog::addLogFromStartTable(startTable, strFmt("@SIMSStartProcessing:RecordProcessing"));
                startRunAction = SIMSStartRunAction::construct(startTable);
                startRunAction.run();
            }
            catch
            {
                if (startTable.ContinueOnError == NoYes::No)
                {
                    SIMSStartLog::addLogFromStartTable(startTable, strFmt("@SIMSStartProcessing:StopProcessingForContinueOnError", startTable.Description, startTable.SortOrder));
                    Error(strFmt("@SIMSStartProcessing:StopProcessingForContinueOnError", startTable.Description, startTable.SortOrder));  // TODO: Stop processing for Continue on error = NO! for record %1 %2
                    return;
                }
            }

            if (startRunAction.parmHasErrors())
            {
                SIMSStartLog::addLogFromStartTable(startTable, strfmt("Line %1 %2 has error", startTable.Description, startTable.SortOrder));
                error(strfmt("Line %1 %2 has error", startTable.Description, startTable.SortOrder));
            }
            else
            {
                SIMSStartLog::addLogFromStartTable(startTable, strfmt("Line %1 %2 processed", startTable.Description, startTable.SortOrder));
                info(strfmt("Line %1 %2 processed", startTable.Description, startTable.SortOrder));
            }
        }
        SIMSStartLog::addLog(strFmt("@SIMSStartProcessing:ProcessedLines", cnt));
        info(strFmt("@SIMSStartProcessing:ProcessedLines", cnt));
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Contains the code that does the actual job of the class.
    /// </summary>
    public void run()
    {
        #OCCRetryCount
        if (! this.validate())
        {
            throw error("Error during validsation");
        }

        this.update();

        SIMSStartTable      startTable;
        ListEnumerator      le = delLine.getEnumerator();
        ttsbegin;
        while (le.moveNext())
        {
            delete_from startTable
                where startTable.RecId == le.current();
        }
        ttscommit;

    }

]]></Source>
			</Method>
			<Method>
				<Name>runsImpersonated</Name>
				<Source><![CDATA[
    public boolean runsImpersonated()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container packedClass)
    {
        Version version = RunBase::getVersion(packedClass);
        ;
        switch (version)
        {
            case #CurrentVersion:
                [version,#CurrentList] = packedClass;
                break;
            default:
                return false;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    public boolean validate(Object _calledFrom = null)
    {
        if (false)
            return checkFailed("");

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    static SIMSStartActionServerRunThread construct()
    {
        return new SIMSStartActionServerRunThread();
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    static ClassDescription description()
    {
        return "AOS Start task processor";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    static void main(Args args)
    {
        SIMSStartActionServerRunThread    startActionServerRunThread;
        ;
        startActionServerRunThread = SIMSStartActionServerRunThread::construct();

        if (startActionServerRunThread.prompt())
        {
            startActionServerRunThread.runOperation();
        }
            
    }

]]></Source>
			</Method>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public final boolean isRetryable()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>